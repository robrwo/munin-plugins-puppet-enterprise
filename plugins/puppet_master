#!/usr/bin/perl

use v5.14.2;

use strict;
use warnings;

use POSIX qw/ mktime /;
use POSIX::strptime qw/ strptime /;

for ( $ARGV[0] ) {

    when ('config') {

        if ( $0 =~ m/count/ ) {

            say "graph_title Puppet Catalog Compilations per Hour";
            say "graph_vlabel Num Compilations";

            say "global_compile_count.label Total Num Compilations";

        } else {

            say "graph_title Puppet Catalog Average Compile Time (Past Hour)";
            say "graph_vlabel Compile Time";

            say "global_avg_compile.label Ave Compile Time";

        }

        say "graph_category puppet";

    }

    default {

        my ( $total, $count ) = process_log();

        if ( $0 =~ m/count/ ) {

            say sprintf( "global_compile_count.value %d", $count ) if $count;

        } else {

            say sprintf( "global_avg_compile.value %1.3f", $total / $count )
                if $count;

        }

    }

}

sub process_log {
    my $log_file    = $ENV{log_file}    || '/var/log/syslog';
    my $time_format = $ENV{time_format} || '%b %d %H:%M:%S';

    my $total = 0;
    my $count = 0;

    open my $fh, '<', $log_file
        or die "Unable to open ${log_file}";

    my $hour = time() - 3600;

    while ( my $line = <$fh> ) {
        next unless $line =~ m{\bpuppet-master\b};

        if ( $line
            =~ m{Compiled catalog for (.+) in environment (.+) in (\d+(?:\.\d+)) seconds}
            )
        {

            my $agent    = $1;
            my $env      = $2;
            my $run_time = $3 + 0;

            my @time = strptime( $line, $time_format );
            $time[5] //= (localtime)[5];

            my $epoch = mktime(@time);

            if ( $epoch > $hour ) {

                $total += $run_time;
                $count++;

            }

        }
    }

    close $fh;

    return ( $total, $count );
}

